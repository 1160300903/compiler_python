Terminate
do if else int boolean float while struct def call true false char print input return
+ - * / > < >= <= == != && || !
( ) { } [ ] ; = , . #
id CI CF CC
Variable
start#拓广文法
P'#产生多个P
P#开始符号
D#定义语句
S#赋值语句
Z#返回语句
W'#生成多个S
Param#参数定义
T#类型信息
X#基本类型
C#数据括号
L#数组引用
E#运算式
Y#运算式
F#运算式
M#运算式
B#逻辑表达式
G#逻辑表达式
H#逻辑表达式
I#逻辑表达式
R#比较运算符
Elist#参数列表
List#输入参数列表
Left#赋值语句左部
M1#标志性非终结符1
M2#标志性非终结符2
M3#标志性非终结符3
M4#标志性非终结符4
Expression
start::=P'
P'::=P' M3 P
P'::=P
P::=D
P::=S


D::=def X id M1 ( Param ) { W' Z }
Z::=return E ;
M1::=ε
W'::=S
W'::=W' M3 S
S::=T id ;
Param::=T id
Param::=Param , T id
T::=X C
T::=struct { M2 Param }
M2::=ε
X::=int
X::=float
X::=boolean
X::=char
C::=C [ CI ]
C::=ε

S::=print ( Elist ) ;
S::=input ( List ) ;

S::=Left = E ;
S::=Left = B ;
Left::=id
Left::=L
Left::=Left . id
L::=id [ E ]
L::=L [ E ]
E::=E + Y
E::=E - Y
E::=Y
Y::=Y * F
Y::=Y / F
Y::=F
F::=( M )
F::=M
M::=Left
M::=CI
M::=CF
M::=CC

S::= if B { M3 W' }
S::= if B { M3 W' } M4 else { M3 W' }
S::= while M3 B do { M3 W' }
M3::=ε
M4::=ε
B::=B || M3 G
B::=G
G::=G && M3 H
G::=H
H::=! H
H::=I
I::=E R E
I::=( B )
I::=true
I::=false
R::=<
R::=<=
R::===
R::=!=
R::=>
R::=>=	

S::=Left = call id ( Elist ) ;
Elist::=Elist , E
Elist::=E
List::=List , id
List::=id